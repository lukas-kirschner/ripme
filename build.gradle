plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}
group 'com.rarchives.ripme'
version '1.7.93'
description 'ripme'
sourceCompatibility = 1.11
project.ext.deploydir = "$projectDir/bin"
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = uri('http://repo.maven.apache.org/maven2')
    }
}
javafx {
    version = "11"
    modules = ['javafx.controls', 'javafx.base']
}
dependencies {
    implementation 'org.jsoup:jsoup:1.8.1'
    implementation 'org.graalvm.js:js:20.1.0'
    implementation 'org.json:json:20190722'
    implementation 'commons-configuration:commons-configuration:1.7'
    implementation 'log4j:log4j:1.2.17'
    implementation 'commons-cli:commons-cli:1.2'
    implementation 'commons-io:commons-io:1.3.2'
    implementation 'org.apache.httpcomponents:httpclient:4.3.6'
    implementation 'org.apache.httpcomponents:httpmime:4.3.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.6.2'
}

task copyRuntimeLibs(type: Copy) {
    into "${deploydir}/lib"
    from configurations.runtimeClasspath
}
compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
task buildJar(type: Jar) {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'com.rarchives.ripme.Main',
                'Class-Path': configurations.runtimeClasspath.collect { 'lib/' + file(it).name }.join(' ')
        )
    }

    archiveFileName = 'ripme-novideos.jar'
    archiveVersion = project.version
    with jar
}

task deployJar(type: Copy) {
    dependsOn copyRuntimeLibs
    dependsOn buildJar
    from buildJar
    into "${deploydir}"
}